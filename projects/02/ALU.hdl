// This file is part of nand2tetris, as taught in The Hebrew University,
// and was written by Aviv Yaish, and is published under the Creative 
// Common Attribution-NonCommercial-ShareAlike 3.0 Unported License 
// https://creativecommons.org/licenses/by-nc-sa/3.0/
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=Proc1X);
    Not16(in=Proc1X, out=NProc1X);
    Mux16(a=Proc1X, b=NProc1X, sel=nx, out=Proc2X);

    Mux16(a=y, b=false, sel=zy, out=Proc1Y);
    Not16(in=Proc1Y, out=NProc1Y);
    Mux16(a=Proc1Y, b=NProc1Y, sel=ny, out=Proc2Y);

    And16(a=Proc2Y, b=Proc2X, out=AndXY);
    Add16(a=Proc2Y, b=Proc2X, out=AddXY);
    Mux16(a=AndXY, b=AddXY, sel=f, out=Prod1);

    Not16(in=Prod1, out=NProd1);
    Mux16(a=Prod1, b=NProd1, sel=no, out[0..7]=Prod2A, out[8..15]=Prod2B,out[15]=ng,out=out);

    Or8Way(in=Prod2A, out=zFlag1);
    Or8Way(in=Prod2B, out=zFlag2);
    Or(a=zFlag1, b=zFlag2, out=Nzr);
    Not(in=Nzr, out=zr);
}